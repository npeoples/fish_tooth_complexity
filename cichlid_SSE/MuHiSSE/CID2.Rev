NUM_TOTAL_SPECIES     = 1154
NUM_SPECIES           = 1069
NUM_STATES            = 4
NUM_HIDDEN            = 2
NUM_RATES             = NUM_STATES * NUM_HIDDEN
H                     = 0.587405

observed_phylogeny <- readTrees("cichlid_tree.nex")[1]
data <- readCharacterData("rate_traits.nex")
data_exp <- data.expandCharacters( NUM_HIDDEN )
taxa <- observed_phylogeny.taxa()
root_age <- observed_phylogeny.rootAge()

mvi = 0
mni = 0

rate_mean <- ln( ln(NUM_SPECIES/2.0) / root_age )
rate_sd ~ dnExponential( 1.0 / H )
moves[++mvi] = mvScale( rate_sd, lambda=1.0, tune=true, weight=2.0 )


speciation_alpha ~ dnNormal(mean=rate_mean,sd=rate_sd)
moves[++mvi] = mvScale(speciation_alpha,lambda=0.1,tune=true,weight=2.0)
moves[++mvi] = mvScale(speciation_alpha,lambda=1.0,tune=true,weight=2.0)
moves[++mvi] = mvSlide(speciation_alpha,delta=2.0,tune=true,weight=2.0)

extinction_alpha ~ dnNormal(mean=rate_mean,sd=rate_sd)
moves[++mvi] = mvScale(extinction_alpha,lambda=0.1,tune=true,weight=2.0)
moves[++mvi] = mvScale(extinction_alpha,lambda=1.0,tune=true,weight=2.0)
moves[++mvi] = mvSlide(extinction_alpha,delta=2.0,tune=true,weight=2.0)

for (i in 1:NUM_HIDDEN){

speciation_beta[i] ~ dnExp(1.0)
moves[++mvi] = mvSlide(speciation_beta[i],delta=2.0,tune=true,weight=2.0)
moves[++mvi] = mvScale(speciation_beta[i],lambda=0.1,tune=true,weight=2.0)
moves[++mvi] = mvScale(speciation_beta[i],lambda=1.0,tune=true,weight=2.0)

extinction_beta[i] ~ dnNormal(0.0,1.0)
moves[++mvi] = mvSlide(extinction_beta[i],delta=2.0,tune=true,weight=2.0)
moves[++mvi] = mvScale(extinction_beta[i],lambda=0.1,tune=true,weight=2.0)
moves[++mvi] = mvScale(extinction_beta[i],lambda=1.0,tune=true,weight=2.0)
}

for (j in 1:NUM_HIDDEN) {
for (i in 1:NUM_STATES) {
if ( j == 1) {
speciation[i] := exp( speciation_alpha )
extinction[i] := exp( extinction_alpha )
} else {
index = i+(j*NUM_STATES)-NUM_STATES
speciation[index] := speciation[index-NUM_STATES] * exp( speciation_beta[j-1] )
extinction[index] := exp( extinction_alpha + extinction_beta[j-1] )
}
}
}

rate_pr := observed_phylogeny.treeLength() / 20
for ( i in 1:(NUM_STATES*(NUM_STATES-1)) ) {
    transition_rates[i] ~ dnExp(rate_pr)
    moves[++mvi] = mvScale(transition_rates[i],lambda=0.5,tune=true,weight=2.0)
}

for (i in 1:(NUM_HIDDEN * (NUM_HIDDEN - 1))) {
    R[i] ~ dnExp(rate_pr)
    moves[++mvi] = mvScale(R[i],lambda=0.5,tune=true,weight=2.0) 
}

rate_matrix := fnHiddenStateRateMatrix(transition_rates, R, rescaled=false)

root_prior ~ dnDirichlet( rep(1,NUM_RATES) )
moves[++mvi] = mvDirichletSimplex(root_prior,tune=true,weight=2)
moves[++mvi] = mvBetaSimplex(root_prior,tune=true,weight=2)

rho <- observed_phylogeny.ntips()/NUM_TOTAL_SPECIES

timetree ~ dnCDBDP( rootAge = root_age,
speciationRates   = speciation,
extinctionRates   = extinction,
Q                 = rate_matrix,
pi                = root_prior,
delta             = 1.0,
rho               = rho)

timetree.clamp( observed_phylogeny )
timetree.clampCharData( data_exp )
mymodel = model(rate_matrix)

monitors[++mni] = mnModel(filename="CID/CID.log", printgen=1)
monitors[++mni] = mnJointConditionalAncestralState(tree=timetree, cdbdp=timetree, type="NaturalNumbers", printgen=200, withTips=true, withStartStates=false, filename="CID/CID_anc_states.log")
monitors[++mni] = mnStochasticCharacterMap(cdbdp=timetree, printgen=200, filename="CID/CID_stoch_map.log", include_simmap=true)
monitors[++mni] = mnScreen(printgen=1, speciation, extinction)

### PP AND STEPPING STONE ###
pow_p = powerPosterior(mymodel, moves, monitors, "CID/CID_pp.out", cats=10) 
pow_p.burnin(generations=5000,tuningInterval=500)
pow_p.run(generations=1000)
ss = steppingStoneSampler(file="CID/CID_pp.out", powerColumnName="power", likelihoodColumnName="likelihood")
ss.marginal()


