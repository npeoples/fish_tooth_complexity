setOption("useScaling", "true")

NUM_TOTAL_SPECIES     = 300
NUM_SPECIES           = 169
NUM_STATES            = 2
NUM_RATES             = NUM_STATES
H                     = 0.587405

observed_phylogeny <- readTrees("Victoria_tree.nex")[1]
data <- readCharacterData("Victoria_traits.nex")
taxa <- observed_phylogeny.taxa()
root_age <- observed_phylogeny.rootAge()

mvi = 0
mni = 0

rate_mean <- ln( ln(NUM_SPECIES/2.0) / root_age )
rate_sd ~ dnExponential( 1.0 / H )
moves[++mvi] = mvScale( rate_sd, lambda=1.0, tune=true, weight=2.0 )

for (i in 1:NUM_STATES) {

    log_speciation[i] ~ dnNormal(mean=rate_mean, sd=rate_sd)
    speciation[i] := exp(log_speciation[i])
    moves[++mvi] = mvSlide(log_speciation[i],delta=0.20,tune=true,weight=3.0)
    moves[++mvi] = mvScale(log_speciation[i],lambda=1.0,tune=true,weight=3.0)    

    log_extinction[i] ~ dnNormal(mean=rate_mean, sd=rate_sd)
    extinction[i] := exp(log_extinction[i])
    moves[++mvi] = mvSlide(log_extinction[i],delta=0.20,tune=true,weight=3.0)
    moves[++mvi] = mvScale(log_extinction[i],lambda=1.0,tune=true,weight=3.0)     

}

rate_pr := observed_phylogeny.treeLength() / 50
for ( i in 1:(NUM_STATES*(NUM_STATES-1)) ) {
    transition_rates[i] ~ dnExp(rate_pr)
    moves[++mvi] = mvScale(transition_rates[i],lambda=1.0,tune=true,weight=2.0)
    moves[++mvi] = mvScale(transition_rates[i],lambda=0.1,tune=true,weight=2.0)
}

rate_matrix := fnFreeBinary(transition_rates, rescaled=false)

root_prior ~ dnDirichlet( rep(1,NUM_RATES) )
moves[++mvi] = mvDirichletSimplex(root_prior,tune=true,weight=2.0)
moves[++mvi] = mvBetaSimplex(root_prior,tune=true,weight=2.0)

rho <- observed_phylogeny.ntips()/NUM_TOTAL_SPECIES

timetree ~ dnCDBDP( rootAge = root_age,
speciationRates   = speciation,
extinctionRates   = extinction,
Q                 = rate_matrix,
pi                = root_prior,
delta             = 1.0,
nTimeSlices       = 2000,
rho               = rho)

timetree.clamp( observed_phylogeny )
timetree.clampCharData( data )
mymodel = model(rate_matrix)

monitors[++mni] = mnModel(filename="Victoria_Final/Victoria_BiSSE.log", printgen=1)
monitors[++mni] = mnJointConditionalAncestralState(tree=timetree, cdbdp=timetree, type="Standard", printgen=1, withTips=true, withStartStates=false, filename="Victoria_Final/Victoria_BiSSE_anc_states.log")
monitors[++mni] = mnStochasticCharacterMap(cdbdp=timetree, printgen=1, filename="Victoria_Final/Victoria_BiSSE_stoch_map.log", include_simmap=true)
monitors[++mni] = mnScreen(printgen=1, speciation, extinction)

### PP AND STEPPING STONE ###
pow_p = powerPosterior(mymodel, moves, monitors, "Victoria_Final/BiSSE_stones/BiSSE.out", cats=50) 
pow_p.burnin(generations=5000,tuningInterval=500)
pow_p.run(generations=1000)
ss = steppingStoneSampler(file="Victoria_Final/BiSSE_stones/BiSSE.out", powerColumnName="power", likelihoodColumnName="likelihood")
ss.marginal()


